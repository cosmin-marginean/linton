plugins {
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
    id "org.jetbrains.kotlin.kapt" version "1.6.21"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.micronaut.application" version "2.0.6"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.6.10"
    id "com.glovoapp.semantic-versioning" version "1.1.10"
}

group = "com.linton"

micronaut {
    runtime("netty")
    processing {
        incremental(true)
        annotations("com.linton.*")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(":linton-utils")
    implementation project(":linton-model")

    kapt 'io.micronaut:micronaut-validation'
    kapt 'io.micronaut:micronaut-http-validation'
    kapt 'io.micronaut:micronaut-inject-java'

    kaptTest 'io.micronaut:micronaut-inject-java'

    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.21'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21'

    implementation 'io.micronaut:micronaut-http-client'
    implementation 'io.micronaut:micronaut-runtime'
    implementation 'io.micronaut.kotlin:micronaut-kotlin-runtime'
    implementation 'io.micronaut.reactor:micronaut-reactor'
    implementation 'io.micronaut.reactor:micronaut-reactor-http-client'
    implementation 'io.micronaut:micronaut-validation'
    implementation 'io.micronaut:micronaut-management'
    implementation "io.micronaut.mongodb:micronaut-mongo-reactive"
    implementation "io.micronaut.mongodb:micronaut-mongo-sync"
    implementation 'io.micronaut.kubernetes:micronaut-kubernetes-client:3.4.0'
    implementation 'io.micronaut.kubernetes:micronaut-kubernetes-discovery-client:3.4.0'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.litote.kmongo:kmongo:4.7.0'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
}

application {
    mainClass.set("com.linton.LintonBookingAppKt")
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
}

shadowJar {
    zip64 true
    manifest {
        attributes('Main-Class': 'com.linton.LintonBookingAppKt')
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    repositories {
        mavenLocal()
    }
}

processResources {
    filesMatching("application.yml") {
        expand LINTON_SERVICE_VERSION: "${project.semanticVersion.version.get()}"
    }
}

run {
    classpath = files('src/main/resources') + files('build') + classpath
    jvmArgs = [
            '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=localhost:5006',
            '-Dmicronaut.environments=local'
    ]
}
